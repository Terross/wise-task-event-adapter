plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id "org.openapi.generator" version "6.6.0"
}

group = 'ru.leti'
version = '1.0.0'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/openApi"
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.apache.kafka:kafka-streams'
    implementation 'org.springframework.kafka:spring-kafka'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'

    //observability
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'net.ttddyy.observation:datasource-micrometer-spring-boot:1.0.1'
    implementation 'io.zipkin.reporter2:zipkin-reporter-brave'
    implementation 'com.github.loki4j:loki-logback-appender:1.3.2'
    implementation 'org.springframework:spring-aspects'
    implementation 'io.zipkin.brave:brave-instrumentation-grpc:5.16.0'

    //OpenAPI dependencies
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.14.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.2'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.0.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.14'
    implementation 'io.swagger:swagger-annotations:1.6.8'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'javax.servlet:javax.servlet-api:4.0.1'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

}

openApiGenerate {
    generatorName.set("spring")
    inputSpec.set("$rootDir/src/main/resources/openapi/wise-task-event-adapter.yaml")
    outputDir.set("$buildDir/generated/openApi")
    apiPackage.set("ru.leti.wise.task.event.adapter.api")
    modelPackage.set("ru.leti.wise.task.event.adapter.model")
    generateApiTests.set(false)
    generateModelTests.set(false)
    generateModelDocumentation.set(false)
    generateApiDocumentation.set(false)
    configOptions.set([
            dateLibrary: "java8",
            delegatePattern: "true",
    ])
}

tasks.openApiGenerate {
    doLast {
        delete (
                "$buildDir/generated/openApi/.openapi-generator",
                "$buildDir/generated/openApi/pom.xml",
                "$buildDir/generated/openApi/README.md",
                "$buildDir/generated/openApi/src/test/",
                "$buildDir/generated/openApi/src/main/java/org",
                "$buildDir/generated/openApi/src/main/resources",
        )
    }
}

compileJava.dependsOn tasks.openApiGenerate


tasks.named('test') {
    useJUnitPlatform()
}
